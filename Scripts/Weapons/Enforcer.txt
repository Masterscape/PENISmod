//This the version with reloading, if you will like to use it,
//just swap this file with DECORATE one.

ACTOR Enforcer : Weapon 20001
{
   Inventory.PickupSound "misc/w_pkup"
   Inventory.PickupMessage "You got the ''Enforcer'' Pistol"
   Obituary "%o was forced to die by %k's ''Enforcer''."
   Weapon.AmmoType1 "EnforcerClip"
   Weapon.AmmoType2 "Clip"
   Weapon.AmmoGive1 0
   Weapon.AmmoGive2 0
   Weapon.AmmoUse1 1
   Weapon.AmmoUse2 0
   Weapon.SlotNumber 2
   Weapon.Kickback 75
   Weapon.UpSound "weapons/enforcerup"
   AttackSound "weapons/enforcerfire"
   +AMMO_OPTIONAL
   +NOALERT
   Scale 0.4
   States
   {
   Spawn:
      LIBG X -1
      Loop
   Ready:
      LIBG A 1 A_WeaponReady
      Loop
   Deselect:
      LIBG A 1 A_Lower
      LIBG A 0 A_Lower
      Loop
   Select:
      LIBG A 1 A_Raise
      LIBG A 0 A_Raise
      Loop
   Fire:
      LIBF A 0 A_JumpIfNoAmmo("DryFire")
      LIBF A 0 A_GunFlash
      LIBF A 0 A_AlertMonsters
      LIBF A 0 A_FireBullets(2,3,1,7,"BulletPuff",1)
      LIBF AB 1 BRIGHT
      LIBG CEDCBA 1
      LIBG A 0 A_ReFire
      Goto Ready
   Flash:
       TNT1 A 2 A_Light2
       TNT1 A 1 A_Light1
       TNT1 A 0 A_Light0
       Stop
   DryFire:
      LIBG A 12 A_PlaySound("weapons/enforcerdry")
      Goto Reload
   AltFire:
   Reload:
      LIBG A 1
      LIBG A 0 A_JumpIfInventory("EnforcerClip", 12, "Ready")
      LIBG A 0 A_JumpIfInventory("Clip", 1, 1)
      Goto Ready
      LIBG A 0 A_PlaySound("weapons/enforcerout")
      LIBM ABCDEF 1
      LIBG A 3
      Goto ReloadWorking
   ReloadWorking:
      TNT1 A 0 A_TakeInventory("Clip", 1)
      TNT1 A 0 A_GiveInventory("EnforcerClip", 1)
      TNT1 A 0 A_JumpIfInventory("EnforcerClip", 12,2)
      TNT1 A 0 A_JumpIfInventory("Clip",1,2)
      TNT1 A 0
      Goto ReloadFinish
      TNT1 A 0
      Goto ReloadWorking
   ReloadFinish:
      LIBR ABCDE 2
      LIBR F 0 A_PlaySound("weapons/enforcerin")
      LIBR FGHIJJ 2
      LIBE ABCDEFGH 1
      LIBG A 3
      Goto Ready
   }
}

ACTOR EnforcerClip : Ammo
{
Inventory.MaxAmount 12
Ammo.BackpackAmount 0
Ammo.BackpackMaxAmount 12
Inventory.Icon LIBGX0
}
